apply plugin: 'com.android.application'
apply plugin: 'com.akaita.android.easylauncher'

android {
    compileSdkVersion = 32

    defaultConfig {
        applicationId "com.deploygate.sample"
        minSdkVersion 19
        targetSdkVersion 32
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        distribute {
            debuggable true
            matchingFallbacks = ['release']
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt'
        }
    }

    flavorDimensions "mode"

    productFlavors {
        devreal {
            dimension "mode"
            applicationIdSuffix ".realsdk.dev"
        }
        devmock {
            dimension "mode"
            applicationIdSuffix ".mocksdk.dev"
        }
        stablereal {
            dimension "mode"
            applicationIdSuffix ".realsdk.stable"
        }
        stablemock {
            dimension "mode"
            applicationIdSuffix ".mocksdk.stable"
        }
    }

    lintOptions {
        abortOnError false
    }
}
easylauncher {
    defaultFlavorNaming = true

    productFlavors {
        devreal {
            filters = customColorRibbonFilter("devreal", "#55E74C3C", "#FFFFFF", "topRight")
        }
        devmock {
            filters = customColorRibbonFilter("devmock", "#556600CC", "#FFFFFF", "topRight")
        }
        stablereal {
            enable false
        }
        stablemock {
            enable false
        }
    }
    
    buildTypes {
        debug {
            filters = customColorRibbonFilter("debug", "#5574924", "#FFFFFF", "bottom")
        }
        distribute {
            enable false
        }
        release {
            enable false
        }
    }
}

dependencies {
    devrealImplementation project(':sdk')
    devmockImplementation project(':sdkMock')

    def version = "4.3.0"
    stablerealImplementation "com.deploygate:sdk:$version"
    stablemockImplementation "com.deploygate:sdk-mock:$version"
}
