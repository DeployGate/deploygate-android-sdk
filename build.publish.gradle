apply plugin: 'signing'
apply plugin: 'maven'

boolean validProperty(propertyName) {
    try { project.property(propertyName) != null }
    catch (MissingPropertyException) { false }
}
assert validProperty('signing.keyId'),             'properties for signing must be provided'
assert validProperty('signing.password'),          'properties for signing must be provided'
assert validProperty('signing.secretKeyRingFile'), 'properties for signing must be provided'
assert validProperty('sonatypePassword'),          'properties for signing must be provided'
assert validProperty('sonatypeUsername'),          'properties for publish must be provided'
assert validProperty('sonatypeFullname'),          'properties for publish must be provided'

signing {
    sign configurations.archives
}

//Defines JavaDocs JavaDocs
task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.getSrcDirs()
}

//Compiles JavaDocs to Jar for maven
task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    //basename = artifact_id
    from androidJavadocs.destinationDir
}

//Compiles Sources to Jar for maven
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    //basename = artifact_id
    from android.sourceSets.main.java.getSrcDirs()
}

// Adds the Source and Javadocs to Maven upload task
artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        pom.project {
            name 'DeployGate SDK'
                packaging 'jar' // We upload an jar as the primarly as you are not defining any resources
                description 'Android SDK for DeployGate'
                url 'https://github.com/DeployGate/deploygate-android-sdk'
                scm {
                    url 'git@github.com:DeployGate/deploygate-android-sdk.git'
                    connection 'scm:git:git@github.com:DeployGate/deploygate-android-sdk.git'
                    developerConnection 'scm:git:git@github.com:DeployGate/deploygate-android-sdk.git'
                }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id sonatypeUsername
                    name sonatypeFullname
                }
            }
        }
    }
}
