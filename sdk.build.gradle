apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

version = rootProject.ext.releaseVersion

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName project.version
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'org.robolectric:robolectric:4.3.1'

    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'com.google.truth:truth:1.0'
}

android.libraryVariants.configureEach { variant ->
    tasks.register("verifyBytecodeVersion${variant.name.capitalize()}", Exec).configure {
        dependsOn "assemble${variant.name.capitalize()}"

        commandLine rootProject.file("scripts/verify-bytecode-version").path, "--aar", project.file("build/outputs/aar/${project.name}-${variant.name}.aar"), "--java", project.android.compileOptions.targetCompatibility.toString()
    }
}