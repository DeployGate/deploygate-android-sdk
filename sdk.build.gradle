apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

version = rootProject.ext.releaseVersion

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName project.version
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'org.robolectric:robolectric:4.3'

    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'com.google.truth:truth:1.0'
}

def pomConfig = {
    name project.ext.displayName
    description project.publish.desc
    url project.publish.website
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "DeployGate"
        }
    }

    scm {
        url 'https://github.com/DeployGate/deploygate-android-sdk'
    }
}

project.plugins.withType(MavenPublishPlugin) {
    def publishingExtension = project.extensions.findByType(PublishingExtension)

    publishingExtension.publications.withType(MavenPublication).whenObjectAdded {
        if (name == "release") {
            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}