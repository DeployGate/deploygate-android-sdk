apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.getProperties().get("bintrayUser")
    key = project.getProperties().get("bintrayKey")

    pkg {
        repo = 'maven'
        name = project.ext.artifactId
        userOrg = 'deploygate'
        licenses = ['Apache-2.0']
    }

    publications = ['release']
}

afterEvaluate {
    android.libraryVariants.configureEach { variant ->
        project.task("generateJavadocFor${variant.name.capitalize()}Publication", type: Javadoc) {
            source = variant.getJavaCompileProvider().get().source
            classpath = variant.getJavaCompileProvider().get().classpath + files(project.android.getBootClasspath())
            options.linkSource true
        }

        project.task("generateSourcesJarFor${variant.name.capitalize()}Publication", type: Jar) {
            archiveClassifier.set('sources')
            from variant.sourceSets.collect { it.javaDirectories }.flatten()
        }

        project.task("generateJavadocsJarFor${variant.name.capitalize()}Publication", type: Jar) {
            archiveClassifier.set('javadoc')
            from files(tasks.getByName("generateJavadocFor${variant.name.capitalize()}Publication"))
        }

        project.tasks.findByName("assemble${variant.name.capitalize()}").dependsOn(
                "generateSourcesJarFor${variant.name.capitalize()}Publication",
                "generateJavadocsJarFor${variant.name.capitalize()}Publication"
        )
    }

    publishing {
        publications {
            def pomConfig = {
                name project.ext.displayName
                description project.ext.desc
                url 'https://github.com/DeployGate/deploygate-android-sdk'
                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        name "DeployGate"
                    }
                }

                scm {
                    url 'https://github.com/DeployGate/deploygate-android-sdk'
                }
            }

            release(MavenPublication) {
                from components.release
                artifact generateSourcesJarForReleasePublication
                artifact generateJavadocsJarForReleasePublication

                groupId = 'com.deploygate'
                artifactId = project.ext.artifactId
                version = project.version

                pom.withXml {
                    asNode().children().last() + pomConfig
                }
            }
        }
    }
}

